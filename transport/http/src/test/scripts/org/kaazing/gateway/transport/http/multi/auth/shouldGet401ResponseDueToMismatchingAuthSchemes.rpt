#
# Copyright 2007-2016, Kaazing Corporation. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#


property validCredentialsFirstRealm ${http:loginBase64Encoder("joe:secretPin")}
property authHeader1 ${http:append("Token ", validCredentialsFirstRealm)}

property validCredentialsSecondRealm ${http:loginBase64Encoder("joe:welcome")}
property authHeaderSecondRealmWrongScheme ${http:append("Negotiate ", validCredentialsSecondRealm)}
property authHeaderSecondRealmCorrectScheme  ${http:append("Basic ", validCredentialsSecondRealm)}

connect "http://localhost:8008/echo"
connected

write http:method "GET"
write http:header "Authorization" ${authHeader1}
write close

read http:status "401" "Unauthorized"
read http:header "Sec-Challenge-Identity" /(?<secChallengeIdentity>.*)/
read http:header "WWW-Authenticate" "Basic"
read notify FIRST_REQUEST_COMPLETE

connect await FIRST_REQUEST_COMPLETE
        "http://localhost:8008/echo"
connected

write http:method "GET"
write http:header "Sec-Challenge-Identity" ${secChallengeIdentity}
write http:header "Authorization" ${authHeaderSecondRealmWrongScheme}
write close

read http:status "401" "Unauthorized"
read http:header "Sec-Challenge-Identity" /(?<secChallengeIdentity>.*)/
read http:header "WWW-Authenticate" "Basic"
read notify SECOND_REQUEST_COMPLETE

connect await SECOND_REQUEST_COMPLETE
        "http://localhost:8008/echo"
connected

write http:method "GET"
write http:header "Sec-Challenge-Identity" ${secChallengeIdentity}
write http:header "Authorization" ${authHeaderSecondRealmCorrectScheme}
write close

read http:status "200" "OK"
