#
# Copyright 2007-2016, Kaazing Corporation. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#

property sequence ${wse:randomInt(100)}
property client127 ${wse:randomBytes(127)}

connect "http://localhost:8080/path/;e/cb?query"
connected

write http:method "POST"
write http:version "HTTP/1.1"
write http:host
write http:content-length
write http:header "X-WebSocket-Version" "wseb-1.0"
write http:header "X-Sequence-No" ${wse:asString(sequence)}
write http:header "Authorization" "Basic am9lOndlbGNvbWU="
write close

read http:status "201" /.+/
read http:version "HTTP/1.1"
read http:header "Content-Type" /text\/plain;charset=(?i)utf-8/

read /(?<upstream>http:\/\/localhost:8080\/path\/.+)\n/
read /(?<downstream>http:\/\/localhost:8080\/path\/.+)\n/
read notify CREATED
read closed

# Downstream
connect await CREATED
        ${downstream}
connected

write http:method "GET"
write http:version "HTTP/1.1"
write http:host
write http:header "X-Sequence-No" ${wse:asString(sequence + 1)}
write http:content-length
write close

read http:status "200" /.+/
read http:header "Content-Type" "application/octet-stream"
read http:header "Connection" "close"

read [0x80 0x7f] ${client127} 

# Upstream
connect await CREATED
        ${upstream}
connected

write http:method "POST"
write http:version "HTTP/1.1"
write http:host
write http:header "Content-Type" "application/octet-stream"
write http:header "X-Sequence-No" ${wse:asString(sequence + 1)}
write http:content-length

write [0x80 0x7F] ${client127}
write [0x01 0x30 0x31 0xFF]
write close

read http:status "200" /.+/
read http:version "HTTP/1.1"
read closed
