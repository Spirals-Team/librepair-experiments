PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sn: <http://www.ldbc.eu/ldbc_socialnet/1.0/data/>
PREFIX snvoc: <http://www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>
PREFIX sntag: <http://www.ldbc.eu/ldbc_socialnet/1.0/tag/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dbpedia: <http://dbpedia.org/resource/>
PREFIX dbpedia-owl: <http://dbpedia.org/ontology/>

SELECT
    (?title AS ?forumTitle)
    (SUM(?hasPost) AS ?postCount)
WHERE
{ 
    {
        SELECT DISTINCT
            ?fr
            ?frId
        WHERE
        {       
            BIND( "$personId"^^xsd:long AS ?rootId )
            ?rootPerson a snvoc:Person .
            ?rootPerson snvoc:id ?rootId .
            ?rootPerson ((snvoc:knows/snvoc:hasPerson)|^(snvoc:knows/snvoc:hasPerson))?/((snvoc:knows/snvoc:hasPerson)|^(snvoc:knows/snvoc:hasPerson)) ?fr .
            ?fr snvoc:id ?frId .
        }    
    } .
    ?forum a snvoc:Forum .
    ?mem snvoc:hasPerson ?fr . 
    ?forum snvoc:hasMember ?mem .
    ?forum snvoc:id ?forumId .
    ?mem snvoc:joinDate ?date .
    FILTER( ?date >= xsd:dateTime("$date") ) .
    OPTIONAL {
        ?post snvoc:hasCreator ?fr .
        ?group snvoc:containerOf ?post .
        BIND( 1 AS ?hasPostInner )      
    } .
    BIND( COALESCE(?hasPostInner,0) AS ?hasPost )
    ?forum snvoc:title ?title . 
}
GROUP BY ?title ?forumId
ORDER BY DESC(?postCount) ?forum
LIMIT 20
