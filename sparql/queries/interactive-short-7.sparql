PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sn: <http://www.ldbc.eu/ldbc_socialnet/1.0/data/>
PREFIX snvoc: <http://www.ldbc.eu/ldbc_socialnet/1.0/vocabulary/>
PREFIX sntag: <http://www.ldbc.eu/ldbc_socialnet/1.0/tag/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dbpedia: <http://dbpedia.org/resource/>
PREFIX dbpedia-owl: <http://dbpedia.org/ontology/>

SELECT
    ?comment
    ?content
    ?creationDate
    ?creator
    ?firstName
    ?lastName
    ?knowsFlag
WHERE
{
    BIND( "$messageId"^^xsd:long AS ?messageId )
    ?message snvoc:id ?messageId .
    ?message snvoc:hasCreator ?messageCreator .
    ?messageCreator snvoc:id ?messageCreatorId .
    ?comment snvoc:replyOf ?message .
    ?comment a snvoc:Comment .
    ?comment snvoc:content ?content .
    ?comment snvoc:creationDate ?creationDate .
    ?comment snvoc:hasCreator ?creator .
    ?creator snvoc:firstName ?firstName .
    ?creator snvoc:lastName ?lastName .
    OPTIONAL {
        ?messageCreator ((snvoc:knows/snvoc:hasPerson)|^(snvoc:knows/snvoc:hasPerson)) ?creator .
        BIND( "true"^^xsd:boolean AS ?knowsFlagInner ) .
    }
    BIND( COALESCE(?knowsFlagInner, "false"^^xsd:boolean) AS ?knowsFlag ) .
}
ORDER BY DESC(?creationDate) ?messageCreatorId
